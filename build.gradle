apply plugin: 'java-library'
apply plugin: 'maven-publish'
apply plugin: 'signing'

sourceCompatibility = 1.8
targetCompatibility = 1.8

group = 'org.xblackcat.sjpu'
def projectTitle = 'SJPU DB Access helper'

version = new File('build.txt').getText('UTF-8')
ext.commonManifest = manifest {
    attributes(
            "Implementation-Title": projectTitle,
            "Implementation-Version": version
    )
}

jar {
    manifest = project.manifest {
        from commonManifest
    }
}

defaultTasks 'clean', 'buildAll'

compileJava.options.encoding = 'UTF-8'
tasks.withType(JavaCompile) {
    options.encoding = 'UTF-8'
}

repositories {
    mavenLocal()
    mavenCentral()
}

dependencies {
    api group: 'org.xblackcat.sjpu', name: 'sjpu-rcb', version: '[0.3.1,1.0)'
    api group: 'org.xblackcat.sjpu', name: 'sjpu-utils', version: '[0.1.5,1.0)'
    api group: 'org.apache.commons', name: 'commons-dbcp2', version: '[2.2,3.0)'
    
    testImplementation group: 'org.apache.logging.log4j', name: 'log4j-core', version: '[2.1.1,3.0)'
    testImplementation group: 'org.apache.logging.log4j', name: 'log4j-jcl', version: '[2.4,3.0)'
    testImplementation group: 'junit', name: 'junit', version: '[4.12,)'
    testImplementation group: 'com.h2database', name: 'h2', version: '[1.4,2.0)'
}

task sourcesJar(type: Jar) {
    from sourceSets.main.allJava
    classifier = 'sources'
    manifest = project.manifest {
        from commonManifest
    }
}

javadoc {
    title = projectTitle
}

task javadocJar(dependsOn: javadoc, type: Jar) {
    from javadoc.outputs.files
    classifier = 'javadoc'
    manifest = project.manifest {
        from commonManifest
    }
}

//[compileJava, compileTestJava]*.options.collect {options ->
//  options.deprecation = true
//  options.compilerArgs.add '-Xlint'
//  options.compilerArgs.add '-Xlint:-unchecked'
//  options.compilerArgs.add '-Xlint:-serial'
//  options.compilerArgs.add '-Xlint:-rawtypes'
//}


task sourcesZip(type: Zip) {
    from project.projectDir
    include 'src/**'
    include 'test/**'
    include 'build.*'
    include 'LICENSE'
    into('lib') {
        from configurations.testImplementation
        include '*'
    }
    classifier = 'sources'
}

task binZip(dependsOn: 'jar', type: Zip) {
    from jar.outputs.files
    into('dep') {
        from configurations.api
    }
    classifier = 'bin'
}

sourceSets {
    main {
        java {
            srcDir 'src/java'
        }
    }
    test {
        java {
            srcDir 'test/java'
        }
        resources {
            srcDir 'test/res'
        }
    }
}

publishing {
    publications {
        mavenJava(MavenPublication) {
            from components.java
            artifact javadocJar
            artifact sourcesJar

            pom {
                name = projectTitle
                packaging = 'jar'
                description = 'Helper for build generator to implement class logic in runtime basing on interface/methods/annotations'
                url = 'https://github.com/xBlackCat/sjpu-rcb'

                scm {
                    url = 'scm:git@github.com:xBlackCat/sjpu-rcb.git'
                    connection = 'scm:git@github.com:xBlackCat/sjpu-rcb.git'
                    developerConnection = 'scm:git@github.com:xBlackCat/sjpu-rcb.git'
                }

                licenses {
                    license {
                        name = 'The Apache Software License, Version 2.0'
                        url = 'http://www.apache.org/licenses/LICENSE-2.0.txt'
                        distribution = 'repo'
                    }
                }

                developers {
                    developer {
                        id = 'xBlackCat'
                        name = 'Zhukov Alexei'
                        email = 'xblackcat@gmail.com'
                    }
                }
            }

        }
    }
    repositories {
        mavenLocal()
        maven {
            url = mavenCentralUrl
            credentials {
                username = mavenCentralUser
                password = mavenCentralPassword
            }
        }
    }
}

signing {
    sign publishing.publications.mavenJava
}

task buildAll(dependsOn: ['build', 'sourcesJar', 'binZip', 'sourcesZip', 'javadocJar']) {}