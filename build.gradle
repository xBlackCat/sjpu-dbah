plugins {
    id 'java-library'
    id 'maven-publish'
    id 'signing'
}

java {
    sourceCompatibility = JavaVersion.VERSION_17
    targetCompatibility = JavaVersion.VERSION_17
}

group = 'org.xblackcat.sjpu'
def projectTitle = 'SJPU DB Access helper'

version = new File('build.txt').getText('UTF-8')
ext.commonManifest = java.manifest {
    attributes(
            "Implementation-Title": projectTitle,
            "Implementation-Version": version
    )
}

jar {
    manifest = java.manifest {
        from commonManifest
    }
}

defaultTasks 'clean', 'buildAll'

tasks.withType(JavaCompile).configureEach {
    options.encoding = 'UTF-8'
}

repositories {
    mavenLocal()
    mavenCentral()
}

dependencies {
    api 'org.xblackcat.sjpu:sjpu-rcb:[0.4,1.0)'
    api 'org.xblackcat.sjpu:sjpu-utils:[0.2,1.0)'
    api 'org.apache.commons:commons-dbcp2:[2.9.0,3.0)'

    testImplementation 'com.h2database:h2:2.1.214'
    testImplementation 'org.junit.jupiter:junit-jupiter:5.10.0'
    testImplementation 'org.junit.jupiter:junit-jupiter-params:5.10.0'
    testRuntimeOnly 'org.apache.logging.log4j:log4j-core:2.20.0'
    testRuntimeOnly 'org.apache.logging.log4j:log4j-jcl:2.20.0'
    testRuntimeOnly 'org.junit.platform:junit-platform-launcher'
}

tasks.register('sourcesJar', org.gradle.jvm.tasks.Jar) {
    from sourceSets.main.allJava
    archiveClassifier.set('sources')
    manifest = java.manifest {
        from commonManifest
    }
}

javadoc {
    title = projectTitle
}

tasks.register('javadocJar', org.gradle.jvm.tasks.Jar) {
    from javadoc.outputs.files
    archiveClassifier.set('javadoc')
    manifest = java.manifest {
        from commonManifest
    }
}

test {
    useJUnitPlatform()
}

//[compileJava, compileTestJava]*.options.collect {options ->
//  options.deprecation = true
//  options.compilerArgs.add '-Xlint'
//  options.compilerArgs.add '-Xlint:-unchecked'
//  options.compilerArgs.add '-Xlint:-serial'
//  options.compilerArgs.add '-Xlint:-rawtypes'
//}


tasks.register('sourcesZip', Zip) {
    from project.projectDir
    include 'src/**'
    include 'test/**'
    include 'build.*'
    include 'LICENSE'
    into('lib') {
        from configurations.testImplementation
        include '*'
    }
    archiveClassifier.set('sources')
}


sourceSets {
    main {
        java {
            srcDir 'src/java'
        }
    }
    test {
        java {
            srcDir 'test/java'
        }
        resources {
            srcDir 'test/res'
        }
    }
}

publishing {
    publications {
        mavenJava(MavenPublication) {
            from components.java
            artifact javadocJar
            artifact sourcesJar

            pom {
                name = projectTitle
                packaging = 'jar'
                description = 'Service for generating DB access logic in simple way via interfaces and annotations'
                url = 'https://github.com/xBlackCat/sjpu-dbah'

                scm {
                    url = 'scm:git@github.com:xBlackCat/sjpu-dbah.git'
                    connection = 'scm:git@github.com:xBlackCat/sjpu-dbah.git'
                    developerConnection = 'scm:git@github.com:xBlackCat/sjpu-dbah.git'
                }

                licenses {
                    license {
                        name = 'The Apache Software License, Version 2.0'
                        url = 'http://www.apache.org/licenses/LICENSE-2.0.txt'
                        distribution = 'repo'
                    }
                }

                developers {
                    developer {
                        id = 'xBlackCat'
                        name = 'Zhukov Alexei'
                        email = 'xblackcat@gmail.com'
                    }
                }
            }

        }
    }
    repositories {
        mavenLocal()
        maven {
            url = mavenCentralUrl
            credentials {
                username = mavenCentralUser
                password = mavenCentralPassword
            }
        }
    }
}

signing {
    sign publishing.publications.mavenJava
}

tasks.register('buildAll') {
    dependsOn 'build'
    dependsOn 'sourcesJar'
    dependsOn 'sourcesZip'
    dependsOn 'javadocJar'
}