plugins {
    id 'java-library'
    id 'maven-publish'
    id 'signing'
    id 'org.gradlex.java-module-dependencies' version "1.1"
    id 'org.gradlex.java-module-testing' version "1.0"
    id 'org.gradlex.extra-java-module-info' version "1.1"
}


sourceCompatibility = JavaVersion.VERSION_17
targetCompatibility = JavaVersion.VERSION_17

group = 'org.xblackcat.sjpu'
def projectTitle = 'SJPU DB Access helper'

version = new File('build.txt').getText('UTF-8')
ext.commonManifest = manifest {
    attributes(
            "Implementation-Title": projectTitle,
            "Implementation-Version": version
    )
}

jar {
    manifest = project.manifest {
        from commonManifest
    }
}

defaultTasks 'clean', 'buildAll'

tasks.withType(JavaCompile).configureEach {
    options.encoding = 'UTF-8'
}

repositories {
    mavenLocal()
    mavenCentral()
}

extraJavaModuleInfo {
    automaticModule("commons-logging:commons-logging", "org.apache.commons.logging")
    automaticModule("org.apache.commons:commons-dbcp2", "org.apache.commons.dbcp2")
}

javaModuleDependencies {
    moduleNameToGA.put("org.apache.commons.pool2", "org.apache.commons:commons-pool2")
//    moduleNameToGA.put("org.apache.commons.dbcp2", "org.apache.commons:commons-dbcp2")
    moduleNameToGA.put('org.javassist', 'org.javassist:javassist')
    moduleNameToGA.put('org.apache.commons.text', 'org.apache.commons:commons-text')
}

dependencies {
    api group: 'org.xblackcat.sjpu', name: 'sjpu-rcb', version: '[0.4,1.0)'
    api group: 'org.xblackcat.sjpu', name: 'sjpu-utils', version: '[0.2,1.0)'
    api group: 'org.apache.commons', name: 'commons-dbcp2', version: '[2.9.0,3.0)'

    testImplementation group: 'org.apache.logging.log4j', name: 'log4j-core', version: '2.18.0'
    testImplementation group: 'org.apache.logging.log4j', name: 'log4j-jcl', version: '2.18.0'
    testImplementation(platform("org.junit:junit-bom:5.8.2"))
    testImplementation('org.junit.jupiter:junit-jupiter')
    testImplementation('org.junit.jupiter:junit-jupiter-params')
    testImplementation group: 'com.h2database', name: 'h2', version: '2.1.214'
}

tasks.register('sourcesJar', org.gradle.jvm.tasks.Jar) {
    from sourceSets.main.allJava
    archiveClassifier.set('sources')
    manifest = project.manifest {
        from commonManifest
    }
}

javadoc {
    title = projectTitle
}

tasks.register('javadocJar', org.gradle.jvm.tasks.Jar) {
    from javadoc.outputs.files
    archiveClassifier.set('javadoc')
    manifest = project.manifest {
        from commonManifest
    }
}

test {
    useJUnitPlatform()
}

//[compileJava, compileTestJava]*.options.collect {options ->
//  options.deprecation = true
//  options.compilerArgs.add '-Xlint'
//  options.compilerArgs.add '-Xlint:-unchecked'
//  options.compilerArgs.add '-Xlint:-serial'
//  options.compilerArgs.add '-Xlint:-rawtypes'
//}


tasks.register('sourcesZip', Zip) {
    from project.projectDir
    include 'src/**'
    include 'test/**'
    include 'build.*'
    include 'LICENSE'
    into('lib') {
        from configurations.testImplementation
        include '*'
    }
    archiveClassifier.set('sources')
}


sourceSets {
    main {
        java {
            srcDir 'src/java'
        }
    }
    test {
        java {
            srcDir 'test/java'
        }
        resources {
            srcDir 'test/res'
        }
    }
}

publishing {
    publications {
        mavenJava(MavenPublication) {
            from components.java
            artifact javadocJar
            artifact sourcesJar

            pom {
                name = projectTitle
                packaging = 'jar'
                description = 'Service for generating DB access logic in simple way via interfaces and annotations'
                url = 'https://github.com/xBlackCat/sjpu-dbah'

                scm {
                    url = 'scm:git@github.com:xBlackCat/sjpu-dbah.git'
                    connection = 'scm:git@github.com:xBlackCat/sjpu-dbah.git'
                    developerConnection = 'scm:git@github.com:xBlackCat/sjpu-dbah.git'
                }

                licenses {
                    license {
                        name = 'The Apache Software License, Version 2.0'
                        url = 'http://www.apache.org/licenses/LICENSE-2.0.txt'
                        distribution = 'repo'
                    }
                }

                developers {
                    developer {
                        id = 'xBlackCat'
                        name = 'Zhukov Alexei'
                        email = 'xblackcat@gmail.com'
                    }
                }
            }

        }
    }
    repositories {
        mavenLocal()
        maven {
            url = mavenCentralUrl
            credentials {
                username = mavenCentralUser
                password = mavenCentralPassword
            }
        }
    }
}

signing {
    sign publishing.publications.mavenJava
}

tasks.register('buildAll') {
    dependsOn 'build'
    dependsOn 'sourcesJar'
    dependsOn 'sourcesZip'
    dependsOn 'javadocJar'
}